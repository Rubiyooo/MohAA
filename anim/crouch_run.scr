//------------
// Crouch_run
//------------
// Run while crouched.  Used to be called run_hunch.scr.  Makes sure that the character 
// is crouching first, and then makes him run.

//println "Entering crouch_run.scr"

//=================================================
//=================================================
start:

	waitexec anim/default_inithandler.scr
	waitexec anim/smoking.scr::SmokeRemoveCigarette

	//make sure we are crouching...
	// If you're crouching, just start crouch-running.  If you're walking or running, don't 
	// bother stopping in order to crouch down, just crouch while you continue to move.
	waitexec anim/crouch.scr::transition

	self.position = crouchrun
	
	while (1)
	{
		self.blendtime = 0.30

		switch (self.weapongroup)
		{
			pistol:
			rifle:
			mp40:
			mp44:
			bar:
			thompson:
			unarmed:
			bazooka:
				if (self.ground == water)
				{
					self setmotionanim run_rifle_water
					self waittill flaggedanimdone
				}
				else if (self.ground == beach)
				{
					self setmotionanim run_rifle_gundown_beach
					self waittill flaggedanimdone
				}
				else
				{
					if (self.movedir != (0 0 0))
					{
						//println "Crouchrun.scr using motion blend with movedir: " self.movedir
						waitexec anim/motionblend.scr::PlayAnim (self.weapongroup + "_crouchrun_")
					}
					else
					{
						//println "Crouchrun.scr with **NO** motion blend"
						self setmotionanim (self.weapongroup + "_crouchrun_forward")
						self waittill flaggedanimdone
					}
				}

				break
				
			default:
				println "CrouchRun default case for " self.weapongroup " weapongroup - fix Crouchrun.scr"
				self setmotionanim rifle_crouchrun_forward
				self waittill flaggedanimdone
				break
		}
	}
end





