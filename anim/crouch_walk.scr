//-------------
// Crouch_walk
//-------------
// Walk while crouching.  If not crouching, be sure to crouch first.

//println "Entering crouch_walk.scr"

//=================================================
//=================================================
start:

	waitexec anim/default_inithandler.scr
	waitexec anim/smoking.scr::SmokeRemoveCigarette

	//make sure we are crouching...
	// If you're crouching, just start crouch-running.  If you're walking or running, don't 
	// bother stopping in order to crouch down, just crouch while you continue to move.
	waitexec anim/crouch.scr::transition

	self.position = crouchwalk
	
	while (1)
	{
		self.blendtime = 0.30

		switch (self.weapongroup)
		{
			pistol:
				self setmotionanim pistol_crouchwalk
				self waittill flaggedanimdone
				break
			rifle:
			mp40:
			mp44:
			bar:
			thompson:
			bazooka:
				if (self.movedir != (0 0 0))
				{
					//println "Crouchwalk.scr using motion blend with movedir: " self.movedir
					waitexec anim/motionblend.scr::PlayAnim "walk_crouch_"
				}
				else
				{
					//println "Crouchwalk.scr with **NO** motion blend"
					self setmotionanim walk_crouch_forward
					self waittill flaggedanimdone
				}
				break
			
			grenade:
			unarmed:
				if (self.movedir != (0 0 0))
				{
					//println "Crouchwalk.scr using motion blend with movedir: " self.movedir
					waitexec anim/motionblend.scr::PlayAnim (self.weapongroup + "_crouch_walk_")
				}
				else
				{
					//println "Crouchwalk.scr with **NO** motion blend"
					self setmotionanim (self.weapongroup + "_crouch_walk_forward")
					self waittill flaggedanimdone
				}
				break
					
			default:
				println "CrouchWalk default case for " self.weapongroup " weapongroup - fix Crouchwalk.scr"
				self setmotionanim unarmed_crouch_walk_forward
				self waittill flaggedanimdone
				break
		}
	}

end
